*** Settings ***
Documentation    The aim of this test suite is manipulating TGW pin outs to trigger fault codes
...              and verify those according to SWRS:s and LDS:s. The pin outs will be manipulated
...              with vector software VT-System and the fault codes will be verified with the
...              diagnostic database configured in CANoe.
Suite Setup       Diagnostic Suite Setup
Suite Teardown    Diagnostic Suite Teardown
Test Setup        Diagnostic Test Setup
Test Teardown     Diagnostic Test Teardown

Force Tags        Diagnostic_VTS
...               UTESP
Library           robot.api.logger
Library           Robot/Libs/Common/CANoeTester.py
Library           Robot/Libs/RemoteSoftwareDownload.py
Resource          Robot/Libs/Common/DiagnosticCommonKeywords.txt
Resource          Resources/TestSetup_kw.robot
Resource          Resources/TGWRoutines_kw.robot

*** Test Cases ***
VoltageAboveThreshold
    [Documentation]  Sets VBAT to higher than threshold voltage and then checks if fault code is set
    [Setup]    ClearVoltageFault    ${CANNet}    ${DiagNode}    100 sec    Battery Power - Circuit Voltage Above Threshold    basic_setup=False

    [Tags]    VoltageAboveThreshold
    ...       TESP_VTC    REST
    ...       TGW2.0    TGW2.1
    ...       LD_Req-43182
    ...    TEA2Plus_VT
    ...    Bridge_VT
    ...    Bridge_NAT
    ...    TEA2Plus_UD
    ...    Bridge_AVT
    DeviceVoltage    ${CANNet}    ${DiagNode}    Battery Power - Circuit Voltage Above Threshold    32.0    10 sec    100 sec
    [Teardown]    Diagnostic Test Teardown

VoltageBelowThreshold
    [Documentation]  Sets VBAT to lower than threshold voltage and then checks if fault code is set
    [Setup]    ClearVoltageFault    ${CANNet}    ${DiagNode}    100 sec    Battery Power - Circuit Voltage Below Threshold

    [Tags]    VoltageBelowThreshold
    ...       TESP_VTC    REST
    ...       TGW2.0    TGW2.1
    ...       LD_Req-43181
    ...    TEA2Plus_VT
    ...    Bridge_VT
    ...    Bridge_NAT
    ...    TEA2Plus_UD
    ...    Bridge_AVT
    DeviceVoltage    ${CANNet}    ${DiagNode}    Battery Power - Circuit Voltage Below Threshold    10.4    10 sec    100 sec
    [Teardown]    Diagnostic Test Teardown

GsmAntennaCircuitOpen
    [Documentation]  Creates open circuit for GSM antenna and then restores connection to antenna.
    [Setup]    ClearGsmFaults    ${CANNet}    ${DiagNode}    10 sec    TGW2 GSM Antenna - Circuit Open    TGW2 GSM Antenna - Circuit Short To Ground

    [Tags]    GsmAntennaCircuitOpen
    ...       TESP_VTC    REST
    ...       TGW2.0    TGW2.1
    ...       LD_Req-43152
    ...       LD_Req-14718v1
    ...       ProblemReport    11978
    ...    TEA2Plus_VT
    ...    Bridge_VT
    ...    Bridge_NAT
    ...    TEA2Plus_UD
    ...    Bridge_AVT
    DeviceCircuitOpen    ${CANNet}    ${DiagNode}    TGW2 GSM Antenna - Circuit Open    GSM_CTRL_1_RE1    5 sec    10 sec
    [Teardown]    Diagnostic Test Teardown

GsmAntennaCircuitShort
    [Documentation]  Creates short circuit for GSM antenna and then restores connection to antenna.
    [Setup]    ClearGsmFaults    ${CANNet}    ${DiagNode}    10 sec    TGW2 GSM Antenna - Circuit Open    TGW2 GSM Antenna - Circuit Short To Ground

    [Tags]    GsmAntennaCircuitShort
    ...       TESP_VTC    REST
    ...       TGW2.0    TGW2.1
    ...       LD_Req-43154
    ...       LD_Req-14716v1
    ...       ProblemReport    12010
    ...    TEA2Plus_VT
    ...    Bridge_VT
    ...    Bridge_NAT
    ...    TEA2Plus_UD
    ...    Bridge_AVT
    DeviceCircuitShort    ${CANNet}    ${DiagNode}    TGW2 GSM Antenna - Circuit Short To Ground    GSM_CTRL_1_RE1    GSM_CTRL_1_RE2    5 sec    5 sec
    [Teardown]    Diagnostic Test Teardown

GpsAntennaCircuitOpen
    [Documentation]  Creates open circuit for GPS antenna and then restores connection to antenna.
    ...               From start-up and the following 5 minutes detect period 2 seconds. After 5 minutes: 28-30 seconds.
    ...               The increased detection time is needed due to a spontaneous reset of the modem.

    [Setup]    ClearGpsFaults    ${CANNet}    ${DiagNode}    5 sec    TGW2 GPS Antenna - Circuit Open    TGW2 GPS Antenna - Circuit Short To Ground

    [Tags]    GpsAntennaCircuitOpen
    ...       TESP_VTC    REST
    ...       TGW2.0    TGW2.1
    ...       LD_Req-43173    LD_Req-14711    LD_Req-8395    LD_Req-29746    LD_Req-22493    LD_Req-14711
    ...    TEA2Plus_VT
    ...    Bridge_VT
    ...    Bridge_NAT
    ...    TEA2Plus_UD
    ...    Bridge_AVT
    Console    \nStart-up TGW:
    Restart TGW
    Console    \nFrom start-up and the following 5 minutes:
    Console    \n\tVehicleMode to Living
    Bus Set Keypos and VM    Living
    DeviceCircuitOpen    ${CANNet}    ${DiagNode}    TGW2 GPS Antenna - Circuit Open    GPS_CTRL_RE1    2 sec    5 sec
    Sleep      60 sec
    Console    \n\tVehicleMode to Accessory
    Bus Set Keypos and VM    Accessory
    DeviceCircuitOpen    ${CANNet}    ${DiagNode}    TGW2 GPS Antenna - Circuit Open    GPS_CTRL_RE1    2 sec    5 sec
    Sleep      60 sec
    Console    \n\tVehicleMode to PreRunning
    Bus Set Keypos and VM    Pre-running
    DeviceCircuitOpen    ${CANNet}    ${DiagNode}    TGW2 GPS Antenna - Circuit Open    GPS_CTRL_RE1    2 sec    5 sec
    Sleep      60 sec
    Console    \n\tVehicleMode to Running Drive
    Bus Set Keypos and VM     Running
    DeviceCircuitOpen    ${CANNet}    ${DiagNode}    TGW2 GPS Antenna - Circuit Open    GPS_CTRL_RE1    2 sec    5 sec
    Sleep      70 sec
    Console    \nAfter 5 minutes:
    DeviceCircuitOpen    ${CANNet}    ${DiagNode}    TGW2 GPS Antenna - Circuit Open    GPS_CTRL_RE1    30 sec   5 sec
    Console    \nAfter 5 minutes: Do not detect before 28 sec
    Canoe Set Systems Variable    VTS    GPS_CTRL_RE1     Relay    ${1}
    Sleep      22 sec
    ${gsm_oc_val}    Canoe Get Systems Variable    VTS    GPS_CTRL_RE1    Relay
    Console    The actual GPS_CTRL_RE1 value is: ${gsm_oc_val}
    Sleep      3 sec
    Console    Request diagnostic information, should not be True = inactive
    ${diag_resp}    Canoe Diagnostic request    diag_ntw=${CANNet}    diag_dev=${DiagNode}    diag_dtc=TGW2 GPS Antenna - Circuit Open
    Should Not Be True    ${diag_resp}
    Sleep      10 sec
    Console    Request diagnostic information, should be True = fault active
    ${diag_resp}    Canoe Diagnostic request    diag_ntw=${CANNet}    diag_dev=${DiagNode}    diag_dtc=TGW2 GPS Antenna - Circuit Open
    Should Be True    ${diag_resp}
    Canoe Set Systems Variable    VTS    GPS_CTRL_RE1   Relay    ${0}
    Console    The actual GPS_CTRL_RE1 value is: ${gsm_oc_val}
    Sleep      1 sec
    ${gsm_oc_val}    Canoe Get Systems Variable    VTS    GPS_CTRL_RE1    Relay
    ClearGpsFaults    ${CANNet}    ${DiagNode}    60 sec    TGW2 GPS Antenna - Circuit Open    TGW2 GPS Antenna - Circuit Short To Ground
    [Teardown]    Diagnostic Test Teardown

GpsAntennaCircuitShort
    [Documentation]  Creates short circuit for GPS antenna and then restores connection to antenna.
    [Setup]    ClearGpsFaults    ${CANNet}    ${DiagNode}    60 sec    TGW2 GPS Antenna - Circuit Open    TGW2 GPS Antenna - Circuit Short To Ground

    [Tags]    GpsAntennaCircuitShort
    ...       TESP_VTC    REST
    ...       TGW2.0    TGW2.1
    ...       LD_Req-43165
    ...       LD_Req-14709v1
    ...       ProblemReport    11980
    ...    TEA2Plus_VT
    ...    Bridge_VT
    ...    Bridge_NAT
    ...    TEA2Plus_UD
    ...    Bridge_AVT
    DeviceCircuitShort    ${CANNet}    ${DiagNode}    TGW2 GPS Antenna - Circuit Short To Ground    GPS_CTRL_RE1    GPS_CTRL_RE2    5 sec    60 sec
    [Teardown]    Diagnostic Test Teardown

WlanAntennaCircuitOpen
    [Documentation]  Creates open circuit for WLAN antenna and then restores connection to antenna.
    [Setup]    ClearWlanFaults    ${CANNet}    ${DiagNode}    25 sec    TGW2 WLAN Antenna - Circuit Open    TGW2 WLAN Antenna - Circuit Short To Ground

    [Tags]    WlanAntennaCircuitOpen
    ...       UnderDevelopment
    ...       TESP_VTC    REST
    ...       TGW2.0    TGW2.1
    ...       LD_Req-43156
    ...       LD_Req-15770v1
    ...       ProblemReport    11981
    ...    TEA2Plus_VT
    ...    Bridge_VT
    ...    Bridge_NAT
    ...    TEA2Plus_UD
    DeviceCircuitOpen    ${CANNet}    ${DiagNode}    TGW2 WLAN Antenna - Circuit Open    WLAN_CTRL_RE1    20 sec    25 sec
    [Teardown]    Diagnostic Test Teardown

WlanAntennaCircuitShort
    [Documentation]  Creates Short circuit for WLAN antenna and then restores connection to antenna.
    [Setup]    ClearWlanFaults    ${CANNet}    ${DiagNode}    20 sec    TGW2 WLAN Antenna - Circuit Open    TGW2 WLAN Antenna - Circuit Short To Ground

    [Tags]    WlanAntennaCircuitShort
    ...       UnderDevelopment
    ...       TESP_VTC    REST
    ...       TGW2.0    TGW2.1
    ...       LD_Req-43159
    ...       LD_Req-15771v1
    ...       ProblemReport    11982
    ...    TEA2Plus_VT
    ...    Bridge_VT
    ...    Bridge_NAT
    ...    TEA2Plus_UD
    DeviceCircuitShort    ${CANNet}    ${DiagNode}    TGW2 WLAN Antenna - Circuit Short To Ground    WLAN_CTRL_RE1    WLAN_CTRL_RE2    20 sec    20 sec
    [Teardown]    Diagnostic Test Teardown

TachographFault
    [Documentation]  Shuts down Tachograph simulation to generate fault
    [Setup]    ClearTachoFault    ${CANNet}    ${DiagNode}    10 sec    Tachograph Information Interface Circuit - Invalid Serial Data Received

    [Tags]    TachographFault
    ...       TESP_VTC    REST
    ...       TGW2.0    TGW2.1
    ...       LD_Req-43161
    ...       LD_Req-14714v2
    ...       ProblemReport    11983
    ...    TEA2Plus_VT
    ...    Bridge_VT
    ${tacho_dtc}    Set Variable    Tachograph Information Interface Circuit - Invalid Serial Data Received
    TachoFault    ${CANNet}    ${DiagNode}    ${tacho_dtc}    EnvTachoStartStop    60 sec    5 sec
    [Teardown]    Diagnostic Test Teardown

Can1939BusOff
    [Documentation]  Creates open circuit on Vehicle_CAN2_L and Vehicle_CAN2_H
    [Setup]    ClearJ1939Faults    ${CANNet}    ${DiagNode}    10 sec    CAN Error J1939 Link - Bus off

    [Tags]    Can1939BusOff
    ...       LD_Req-43136
    ...       ProblemReport    11984
    ...    Bridge_VT
    CanErrorBusOff    ${CANNet}    ${DiagNode}    RelayOrgComponent    CAN Error J1939 Link - Bus off    Vehicle_CAN2_L    Vehicle_CAN2_H    10 sec    10 sec
    [Teardown]    Diagnostic Test Teardown

J1587BusOff
    [Documentation]  Creates open circuit on J708A and J1708B
    [Setup]    ClearJ1587Faults    ${CANNet}    ${DiagNode}    10 sec    J1587 Link Error - Bus off

    [Tags]    J1587BusOff
    ...       TGW2.0    TGW2.1
    ...       LD_Req-43139
    ...       ProblemReport    11985
    ...    Bridge_VT
    ...    Bridge_NAT
    CanErrorBusOff    ${CANNet}    ${DiagNode}    RelayOrgComponent    J1587 Link Error - Bus off    J1708A    J1708B    10 sec    10 sec
    [Teardown]    Diagnostic Test Teardown

ClearFaultMemory
    [Documentation]  Clears the fault memory and checks if all faults have been cleared

    [Tags]    ClearFaultMemory
    ...       TESP_VTC    REST
    ...       TGW2.0    TGW2.1
    ...    TEA2Plus_VT
    ClearFaults    ${CANNet}    ${DiagNode}
    [Teardown]    Diagnostic Test Teardown

ClearFaultMemoryUD
    [Documentation]  Clears the fault memory and checks if all faults have been cleared

    [Tags]    ClearFaultMemoryUD
    ...       TGW2.1
    ...    TEA2Plus_UD
    ClearFaultsUD    ${CANNet}    ${DiagNode}
    [Teardown]    Diagnostic Test Teardown

ClearFaultMemoryMD
    [Documentation]  Clears the fault memory and checks if all faults have been cleared

    [Tags]    ClearFaultMemoryMD
    ...       TGW2.0    TGW2.1
    ...    Bridge_VT
    ClearFaultsMD    ${CANNet}    ${DiagNode}
    [Teardown]    Diagnostic Test Teardown

ClearFaultMemoryAVT
    [Documentation]  Clears the fault memory and checks if all faults have been cleared

    [Tags]    ClearFaultMemoryAVT
    ...       TGW2.0    TGW2.1
    ...    Bridge_AVT
    ClearFaultsAVT    ${CANNet}    ${DiagNode}
    [Teardown]    Diagnostic Test Teardown

ClearFaultMemoryVL
    [Documentation]  Clears the fault memory and checks if all faults have been cleared

    [Tags]    ClearFaultMemoryVL
    ...       TGW2.0    TGW2.1
    ...    Bridge_NAT
    ClearFaultsVL    ${CANNet}    ${DiagNode}
    [Teardown]    Diagnostic Test Teardown

*** Keywords ***




